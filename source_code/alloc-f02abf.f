      SUBROUTINE F02ABF(A, IA, N, R, V, IV, E, IFAIL)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS SUBROUTINE SIMULATES NAG DIAGONALISER F02ABF WITH LAPACK CALLS
C  JM Hutson MAY 93
C
C  IT DIAGONALISES THE NXN MATRIX A.  
C  THE EIGENVALUES ARE RETURNED IN R. 
C  THE EIGENVECTORS ARE RETURNED IN V
C  THE DIAGONAL ELEMENTS OF A ARE STORED IN E.  
C  IFAIL=0 ON SUCCESSFUL EXIT
C
C
C  ALTERED TO USE ALLOCATABLE ARRAYS
C  CRLS AUG 14
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      ALLOCATABLE::WORK(:),IWORK(:),JFAIL(:)
      DIMENSION A(IA,N), V(IV,N), R(N), E(N)
      DATA ZERO/0.D0/
C
C  SAVE DIAGONAL ELEMENTS IN E
C
      CALL DCOPY(N,A,IA+1,E,1)
C
C  WORK SPACE QUERY FIRST
      ALLOCATE(WORK(1),IWORK(5*N),JFAIL(N))
      CALL DSYEVX('V','A','L',N,A,IA,DUM,DUM,IDUM,IDUM,ZERO,M,R,V,IV,
     1            WORK,-1,IWORK,JFAIL,INFO)
      LWORK=WORK(1)
      DEALLOCATE(WORK)
      ALLOCATE(WORK(LWORK+1))
C  CALL LAPACK DIAGONALISER: DESTROYS LOWER TRIANGLE OF A
C
C  DSYEVX COMPUTES EIGENVALUES AND EIGENVECTORS OF A USING THE LOWER
C  TRIANGLE.  M IS THE TOTAL NUMBER OF EIGENVALUES FOUND, EIGENVALUES
C  ARE RETURNED IN R, EIGENVECTORS IN COLUMNS OF V, WORK IS WORKSPACE
C  DIM LWORK, IWORK IS WORKSPACE DIM 5*N
      CALL DSYEVX('V','A','L',N,A,IA,DUM,DUM,IDUM,IDUM,ZERO,M,R,V,IV,
     1            WORK(2),LWORK,IWORK,JFAIL,INFO)
      DEALLOCATE(WORK,IWORK,JFAIL)
C
      IF (INFO.NE.0) THEN
        WRITE(6,120) INFO
120     FORMAT(' *** ERROR IN DSYEVX: INFO =',I3)
      ENDIF
C
      IFAIL=INFO
C
C  RESTORE LOWER TRIANGLE FROM UNCHANGED UPPER TRIANGLE
C  AND DIAGONAL FROM E
C
      CALL DSYFIL('L',N,A,IA)
      CALL DCOPY(N,E,1,A,IA+1)
C
      RETURN
      END
