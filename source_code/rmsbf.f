      SUBROUTINE RMSBF(N,Z,RATIO)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C   RETURNS RATIO OF DERIVATIVE TO FUNCTION VALUE FOR
C   MODIFIED SPHERICAL BESSEL FUNCTIONS OF THE THIRD KIND,
C   USING RECURSION BASED ON ABRAMOWITZ AND STEGUN 10.2.18.
C   THE FUNCTION INVOLVED IS GN(Z)=(-)**(N+1)*(2*Z/PI)*FN(Z)
C   IN THE NOTATION OF A&S.
C
C   THIS LOOP GENERATES F3=GN+1(Z)/GN(Z) AND F1=GN-1(Z)/GN(Z) USING
C   A&S 10.2.18
      F1=1.D0
      IF (N.EQ.0) GOTO 20
      DO 10 I=1,N
        F3 = F1+DBLE(I+I-1)/Z
        F1 = 1.D0/F3
  10  CONTINUE
  20  F3 = F1+DBLE(N+N+1)/Z

C   THIS RATIO IS DERIV(GN(Z))/GN(Z) USING 10.2.19 AND
C   GN+1(Z)/GN(Z)=-FN+1(Z)/FN(Z),
      RATIO=1.D0/Z-(DBLE(N)*F1+DBLE(N+1)*F3)/DBLE(N+N+1)

      RETURN
      END
