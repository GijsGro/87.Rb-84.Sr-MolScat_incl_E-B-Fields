      SUBROUTINE RSYM(NN,R,STEST,IPRINT)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS ROUTINE CHECKS THAT THE ARRAY R IS SYMMETRIC, IF NOT IT PRINTS
C  OUT THE MAXIMUM DIFFERENCE BETWEEN RELATED ELEMENTS.
C
C  ON ENTRY: R IS ARRAY DIMENSIONED (NN,NN);
C            STEST IS VALUE TO JUDGE NON-SYMMETRY BY;
C            IPRINT CONTROLS VERBOSITY OF OUTPUT.
C  ON EXIT:  R IS SYMMETRIZED VERSION OF R.
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER IPRINT
      DIMENSION R(NN,NN)

      IF (NN.LE.1) RETURN
      NERR=0
      XX=0.D0
      TEST=MAX(STEST,5.D-7)

      DO 1200 I=2,NN
        IM1=I-1
      DO 1200 J=1,IM1
        TOTAL=R(I,J)+R(J,I)
        ATOTAL=ABS(TOTAL)
        ADIF=ABS(R(I,J)-R(J,I))
        IF (ATOTAL.LE.TEST) GOTO 1100
        RAT=ADIF
        IF (ATOTAL.GE.2.D0) RAT=RAT/ATOTAL
        IF (RAT.LE.TEST) GOTO 1100
        XX=MAX(XX,RAT)
        NERR=NERR+1
 1100   TOTAL=.5D0*TOTAL
        R(I,J)=TOTAL
 1200   R(J,I)=TOTAL

      IF (NERR.LE.0) RETURN
      NO=NN*(NN-1)/2
      IF (IPRINT.GE.4) WRITE(6,601) NERR,NO,TEST,XX
  601 FORMAT(I6,' OF',I4,' OFF-DIAGONAL ELEMENTS OF K-MATRIX NOT ',
     1       'SYMMETRIC WITH RESPECT TO TEST =',2E12.4)
      RETURN
      END
