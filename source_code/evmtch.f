      SUBROUTINE EVMTCH(Y,EVEC,PSI,EVAL,WORK,
     1                  JSTATE,L,JSINDX,N,NSTATE,NQN,IPRINT,WAVE)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT NONE
C
C  SUBROUTINE TO FIND THE EIGENVECTOR CORRESPONDING TO A NEAR-ZERO
C  EIGENVALUE AND PRINT IT.
C  THE WAVEFUNCTION AT THE RMID IS THE EIGENVECTOR CORRESPONDING TO THE
C  ZERO EIGENVALUE OF THE LOG-D MATCHING MATRIX
C
C  ON ENTRY: Y CONTAINS THE LOG-DERIVATIVE MATCHING MATRIX,
C            JSINDX CONTAINS THE POINTERS TO THE BASIS FUNCTIONS,
C            L CONTAINS THE L QUANTUM NUMBER FOR EACH BASIS FUNCTION,
C            N IS THE SIZE OF THE BASIS,
C            JSTATE CONTAINS THE BASIS FUNCTION LABELS,
C            NSTATE IS THE SIZE OF THE COMPLETE BASIS,
C            NQN IS THE NUMBER OF QUANTUM LABELS,
C            IPRINT CONTROLS VERBOSITY OF OUTPUT,
C            WAVE CONTROLS WHETHER WAVEFUNCTIONS ARE CALCULATED,
C  DURING:   EVEC IS USED FOR THE EIGENVECTORS OF THE LOG-DERIVATIVE MATRIX,
C            EVAL IS USED FOR THE EIGENVALUES OF THE LOG-DERIVATIVE MATRIX,
C  ON EXIT:  PSI CONTAINS THE EIGENVECTOR CORRESPONDING TO THE SMALLEST
C            EIGENVALUE OF THE MATCHING MATRIX.

      INTEGER, INTENT(IN)::NSTATE,JSTATE(NSTATE,NQN),NQN,IPRINT,L(N),
     1                     JSINDX(N),N
      LOGICAL, INTENT(IN)::WAVE
      DOUBLE PRECISION, INTENT(INOUT)::Y(N,N),EVAL(N),EVEC(N,N),
     1                                 WORK(N*N),PSI(N)

      INTEGER I,IMIN,K,JJ,IFAIL
      DOUBLE PRECISION EMIN,CRIT,P

C  FIND THE EIGENVALUES AND EIGENVECTORS OF THE MATCHING MATRIX
      CALL DIAGVC(Y,N,N,EVAL,EVEC)
C
C     IF (IPRINT.GE.9) WRITE(6,1000) (I,EVAL(I),I=1,N)
C1000 FORMAT(/'  EIGENVALUES OF MATCHING MATRIX ARE:'/(5(I3,G15.7)))
C
C  FIND THE SMALLEST EIGENVALUE OF THE MATCHING MATRIX
      EMIN=1.D30
      DO I=1,N
        IF (ABS(EVAL(I)).GT.EMIN) CYCLE
        IMIN=I
        EMIN=ABS(EVAL(I))
      ENDDO

      IF (IPRINT.GE.5) WRITE(6,1001) IMIN,EVAL(IMIN)
 1001 FORMAT(/'  SMALLEST EIGENVALUE IS NUMBER',I3,' = ',G12.5//
     1       '  WITH PRINCIPAL EIGENVECTOR COMPONENTS'/
     2       '  CHAN     VEC    VEC**2   L   QUANTUM NUMBERS'/)

      CRIT=0.5D0/DBLE(N)
      DO I=1,N
C  COPY THE EIGENVECTOR OF THE SMALLEST EIGENVALUE TO PSI
        IF (WAVE) THEN
          PSI(I)=EVEC(I,IMIN)
        ENDIF
        P=EVEC(I,IMIN)**2
        JJ=JSINDX(I)
C  WRITE OUT (A LIMITED SUBSET OF) THE COMPONENTS OF THIS EIGENVECTOR
        IF (P.GT.CRIT .OR. IPRINT.GE.11)
     1    WRITE(6,1002) I,EVEC(I,IMIN),P,L(I),(JSTATE(JJ,K),K=1,NQN)
 1002     FORMAT(I4,2F10.4,8I4)
      ENDDO
      RETURN
      END
