      SUBROUTINE F02AAF(A, IA, N, R, E, IFAIL)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS SUBROUTINE SIMULATES NAG DIAGONALISER F02AAF WITH LAPACK CALLS
C  JM Hutson MAY 93
C
C  IT FINDS THE EIGENVALUES OF THE NXN MATRIX A
C  THE EIGENVALUES ARE RETURNED IN R
C  THE DIAGONAL ELEMENTS OF A ARE STORED IN E
C  IFAIL=0 ON SUCCESSFUL EXIT
C
C  ALTERED TO USE ALLOCATABLE ARRAYS
C  CRLS AUG 14
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      ALLOCATABLE::WORK(:),IWORK(:),JFAIL(:)
      DIMENSION A(IA,N), R(N), E(N)
      DIMENSION V(1)
      DATA IV/1/
C
C  SAVE DIAGONAL ELEMENTS IN E
C
      CALL DCOPY(N,A,IA+1,E,1)
C
C  ABSTOL IS SET TO 2 * UNDERFLOW THRESHOLD GIVE MAXIMUM ACCURACY
C  UNDERFLOW THRESHOLD OBTAINED FROM LAPACK ROUTINE DLAMCH
C
      ABSTOL=2.D0*DLAMCH('S')
C
C  WORK SPACE QUERY FIRST
C
      ALLOCATE(WORK(1),IWORK(5*N),JFAIL(N))
      CALL DSYEVX('N','A','L',N,A,IA,DUM,DUM,IDUM,IDUM,ABSTOL,M,R,V,IV,
     1            WORK,-1,IWORK,JFAIL,INFO)
      LWORK=WORK(1)
      DEALLOCATE(WORK)
      ALLOCATE(WORK(LWORK))
C
C  CALL LAPACK DIAGONALISER: DESTROYS LOWER TRIANGLE OF A
C
C  DSYEVX COMPUTES EIGENVALUES OF A USING THE LOWER TRIANGLE.  M IS THE
C  TOTAL NUMBER OF EIGENVALUES FOUND, EIGENVALUES ARE RETURNED IN R,
C  WORK IS WORKSPACE DIM LWORK, IWORK IS WORKSPACE DIM 5*N
C
      CALL DSYEVX('N','A','L',N,A,IA,DUM,DUM,IDUM,IDUM,ABSTOL,M,R,V,IV,
     1            WORK,LWORK,IWORK,JFAIL,INFO)
      DEALLOCATE(WORK,IWORK,JFAIL)
C
      IF (INFO.NE.0) THEN
        WRITE(6,120) INFO
120     FORMAT(' *** ERROR IN DSYEVX: INFO =',I3)
      ENDIF
C
      IFAIL=INFO
C
C  RESTORE LOWER TRIANGLE FROM UNCHANGED UPPER TRIANGLE
C  AND DIAGONAL FROM E
C
      CALL DSYFIL('L',N,A,IA)
      CALL DCOPY(N,E,1,A,IA+1)
C
      RETURN
      END
