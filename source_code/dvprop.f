      SUBROUTINE DVPROP(N,NSQ,MXLAM,NPOTL,
     1                  SR,SI,A,VL,IV,EINT,CENT,WV,L,NB,P,
     2                  Y,YP,F,XM,YM,DIAG,
     3                  RMIN,RMAX,HH,ICODE,IPRINT)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  DEVOGELAERE PROPAGATION (DOUBLE PRECISION)
C  INCLUDING START ROUTINE, SUPPRESSION OF CLOSED-CHANNEL GROWTH
C  AND S-MATRIX DETERMINATION IN ASYMPTOTIC LIMIT.
C  FOLLOWS OUTLINE OF PAUL MCGUIRE'S PROGRAM.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL IREAD,IWRITE,LEND
      INTEGER L(N),NB(N),IV(1)
      INTEGER IPRINT
      DIMENSION Y(NSQ,4),YP(NSQ,2),F(NSQ,4),A(NSQ),XM(NSQ),YM(NSQ),
     1          DIAG(N),P(MXLAM),SR(NSQ),SI(NSQ),VL(2),EINT(N),CENT(N),
     2          WV(N),R(4)
C
C  INDICES ON Y, YP, F ARE (ITH SOLN. COMP, NTH SOLN, KTH R-VALUE)
C
C  COMMON FROM DRIVER
      COMMON /DRIVE / STEST,STEPS,STAB,CONV,
     1                DUMMY(10),ESHIFT,ERED,RMLMDA,
     2                NOPEN,JKEEP,ISCRU,MAXSTP,ILDSVU
C
C  STAB  IS NUMBER OF STEPS TAKEN BEFORE STABILIZATION.
C
      CHARACTER(5) TRY(2)
      DATA TRY/'  TRY','TRIES'/
C  MAX. NUMBER OF TRIALS TO CONVERGE S-MATRIX IN ASYMPTOTIC REGION.
      DATA MXSTRY/20/
C
      IREAD  = ICODE.EQ.2 .AND. ISCRU.GT.0
      IWRITE = ICODE.EQ.1 .AND. ISCRU.GT.0
      IF (IREAD .AND. IPRINT.GE.5) WRITE(6,668)
  668 FORMAT(/' DEVOGELAERE PROPAGATION WILL USE STORED INITIAL R,',
     1       ' STEP SIZE AND POTENTIAL MATRICES')
C
C  ZERO STORAGE . . .
C
      NP1=N+1
      DO 700 I=1,4
      DO 700 IJ=1,NSQ
        Y(IJ,I)=0.D0
  700   F(IJ,I)=0.D0
C
      NSTRY=0
      RMSAVE=RMAX
C
C  SUBROUTINE DVSTRT REPLACED BY INITIALISATION FROM YINIT IN NOV 2018
C  SR CONTAINS INITIAL LOG-DERIVATIVE MATRIX:
C  COPY IT TO WAVEFUNCTION DERIVATIVE IN YP(IJ,1) 
C
      DO 800 IJ=1,NSQ
        YP(IJ,1)=SR(IJ)
        YP(IJ,2)=0.D0
        SI(IJ)=0.D0
  800   SR(IJ)=0.D0
C
C  AND SET WAVEFUNCTION IN Y(IJ,2) TO UNIT MATRIX: OFF-DIAGONAL ELEMENTS ALREADY ZERO
C
      IJ=1
      DO 900 I=1,N
        Y(IJ,2)=1.D0
        IJ=IJ+NP1
  900 CONTINUE

C  ********** START PROPAGATION **********
      NSTAB=STAB
      NSTAB=MAX(NSTAB,1)
      H2=HH/2.D0
      R(2)=RMIN
      NSTEP=1
C  GET F(,,2) FROM Y(,,2)
      R4=R(2)
      IF (.NOT.IREAD) GOTO 1200

      READ(ISCRU) A
      DO 1100 IJ=1,NSQ,NP1
 1100   A(IJ)=A(IJ)-ESHIFT
      GOTO 1300

 1200 CALL WAVMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,RMLMDA,DIAG,
     1            MXLAM,NPOTL,IPRINT)
      IF (IWRITE) WRITE(ISCRU) A
 1300 CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,2),N,0.D0,F(1,2),N)
      CALL DAXPY(NSQ,1.D0,F(1,2),1,Y(1,2),1)
      CALL DAXPY(NSQ,-H2,YP(1,1),1,Y(1,1),1)
      CALL DAXPY(NSQ,0.5D0*H2*H2,F(1,2),1,Y(1,1),1)
C  GET F(,,1) FROM THIS Y(,,1).  NEEDS POTENTIAL AT R(1)
      R(1)=R(2)-H2
      R4=R(1)
      IF (.NOT.IREAD) GOTO 1800

      READ(ISCRU) A
      DO 1700 IJ=1,NSQ,NP1
 1700   A(IJ)=A(IJ)-ESHIFT
      GOTO 1900

 1800 CALL WAVMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,RMLMDA,DIAG,
     1            MXLAM,NPOTL,IPRINT)
      IF (IWRITE) WRITE(ISCRU) A
 1900 CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,1),N,0.D0,F(1,1),N)
C
C  **********      MAIN BODY OF ITERATION  **********
C  PROPAGATE FROM (-1/2) AND (0) TO (1/2) AND (1).
 2000 CONTINUE
      CALL DAXPY(NSQ,1.D0,Y(1,2),1,Y(1,3),1)
      CALL DAXPY(NSQ,H2,YP(1,1),1,Y(1,3),1)
      CALL DAXPY(NSQ,H2*H2*4.D0/6.D0,F(1,2),1,Y(1,3),1)
      CALL DAXPY(NSQ,-H2*H2/6.D0,F(1,1),1,Y(1,3),1)
      R(3)=R(2)+H2
      R4=R(3)
      IF (.NOT.IREAD) GOTO 2200
      READ(ISCRU) A
      DO 2100 IJ=1,NSQ,NP1
 2100   A(IJ)=A(IJ)-ESHIFT
      GOTO 2300

 2200 CALL WAVMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,RMLMDA,DIAG,
     1            MXLAM,NPOTL,IPRINT)
 2300 IF (IWRITE) WRITE(ISCRU) A
      CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,3),N,0.D0,F(1,3),N)
      CALL DAXPY(NSQ,1.D0,Y(1,2),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH,YP(1,1),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH*HH/6.D0,F(1,2),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH*HH/3.D0,F(1,3),1,Y(1,4),1)
      R(4)=R(3)+H2
      R4=R(4)
      IF (.NOT.IREAD) GOTO 2700

      READ(ISCRU) A
      DO 2600 IJ=1,NSQ,NP1
 2600   A(IJ)=A(IJ)-ESHIFT
      GOTO 2800

 2700 CALL WAVMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,RMLMDA,DIAG,
     1            MXLAM,NPOTL,IPRINT)
      IF (IWRITE) WRITE(ISCRU) A
 2800 CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,4),N,0.D0,F(1,4),N)
      CALL DAXPY(NSQ,1.D0,YP(1,1),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH/6.D0,F(1,2),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH/6.D0,F(1,4),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH*4.D0/6.D0,F(1,3),1,YP(1,2),1)
      NSTEP=NSTEP+1
C
C  **********      THIS ENDS DEVOGELAERE CYCLE    **********
C
      NOPLOC=0
      DO 2900 I=1,NSQ,NP1
 2900   IF (A(I).LT.0.D0) NOPLOC=NOPLOC+1
C
      LEND=R4.GT.RMAX .AND. NOPLOC.GE.NOPEN
      IF (IREAD) READ(ISCRU) LEND
      IF (LEND) GOTO 3000
C
C  ********** STABILIZATION EVERY NSTAB STEPS     **********
 4000 IF (NSTEP-NSTAB*(NSTEP/NSTAB).NE.0) GOTO 5000

      IF (IPRINT.GE.13) WRITE(6,673) R(4)
  673 FORMAT('  STABILIZATION DONE AT R =',E12.4)
C  FIRST 2 COLS OF Y AND F AND ALSO A USED AS SCRATCH IN STABIL.
      CALL STABIL(N,NB,Y(1,4),YP(1,2),F(1,3),F(1,4),
     &            A,Y(1,1),Y(1,2),F(1,1),F(1,2))
C
C  **********   RE-INITIALIZE FOR NEXT CYCLE OF PROPAGATION *********
 5000 R(1)=R(3)
      R(2)=R(4)
      IF (IWRITE) WRITE(ISCRU) IREAD
      CALL DCOPY(NSQ,YP(1,2),1,YP(1,1),1)
      CALL DCOPY(NSQ,Y(1,3),1,Y(1,1),1)
      CALL DCOPY(NSQ,Y(1,4),1,Y(1,2),1)
      CALL DCOPY(NSQ,F(1,3),1,F(1,1),1)
      CALL DCOPY(NSQ,F(1,4),1,F(1,2),1)
      DO 5200 IJ=1,NSQ
        YP(IJ,2)=0.D0
        Y(IJ,3)=0.D0
 5200   Y(IJ,4)=0.D0
      GOTO 2000
C
 3000 CONTINUE
      IF ((IPRINT.GE.4 .AND. NSTRY.LE.0) .OR. IPRINT.GE.13)
     &  WRITE(6,601) NSTEP,R(4)
  601 FORMAT('  PROPAGATION REACHED ASYMPTOTIC LIMIT IN',
     &       I5,' STEPS.  R =',E12.4)
C
C  ********** ASYMPTOTIC REGION - CALCULATE S-MATRIX     **********
      NOPSQ=NOPEN*NOPEN
C  USE FIRST 2 COLS OF Y AND F FOR REGULAR AND IRREGULAR BESSEL FNS.
C  AND DERIVATIVES - UJ, UJP, UN, AND UNP.
C  USE FIRST COL. OF A FOR  WRONSKIAN/SQRT(WV).
      CALL DVFREE(Y(1,1),Y(1,2),F(1,1),F(1,2),A,L,NOPEN,WV,R(4),NB)
C  FORM TRANSPOSE OF X- AND Y- MATRICES
      DO 3200 J=1,NOPEN
        IJ=J
        DO 3100 I=1,NOPEN
          NX=NB(I)
          NY=NX+N*(J-1)
          XM(IJ)=(F(NX,2)*Y(NY,4)-F(NX,1)*YP(NY,2)) / A(NX)
          YM(IJ)=(Y(NX,2)*Y(NY,4)-Y(NX,1)*YP(NY,2)) / A(NX)
 3100     IJ=IJ+NOPEN
 3200 CONTINUE
C  GET K MATRIX FROM SOLN TO LINEAR EQNS, REPLACES RHS
      DO 3300 I=1,NOPSQ
 3300   A(I)=YM(I)
      CALL DGESV(NOPEN,NOPEN,XM,NOPEN,Y,A,NOPEN,IER)
      IF (IER.EQ.0) GOTO 3400

      WRITE(6,688)
  688 FORMAT(/' * * * WARNING.  LOSS OF ACCURACY IN SOLVING FOR K.')
      CALL OUTERR(11)
C
C  FORCE SYMMETRY ON K MATRIX AND CALCULATE S MATRIX
C
 3400 CALL RSYM(NOPEN, A, STEST, IPRINT)
      CALL KTOS(A,XM,YM,NOPEN)
C
C  TEST FOR CONVERGENCE OF SR, SI
C
      TEST=0.D0
      DO 3500 I=1,NOPSQ
        TEST=MAX(TEST,ABS(SR(I)-XM(I)),ABS(SI(I)-YM(I)))
        SR(I)=XM(I)
 3500   SI(I)=YM(I)
C
      IF (IREAD) GOTO 9000

      IF (TEST.GT.STEST) GOTO 3600

      IF (IPRINT.GE.4) WRITE(6,686) NSTRY,TRY(MIN(NSTRY,2)),R(4),TEST
  686 FORMAT('  S-MATRIX CONVERGED AFTER',I3,' ',A,' IN ',
     &       'ASYMPTOTIC REGION.  R =',E12.4,'.  TEST =',E12.4)
      GOTO 9000

 3600 IF (NSTRY.GT.0 .AND. IPRINT.GE.4)
     1  WRITE(6,687) NSTRY,TRY(MIN(NSTRY,2)),R(4),TEST
  687 FORMAT('  S-MATRIX NOT CONVERGED AFTER',I3,' ',A,'.  R =',E12.4,
     1       '.  LARGEST CHANGE =',E12.4)
      IF (NSTRY.LT.MXSTRY) GOTO 3700

C  SET 'CONV' FLAG FOR OUTPUT ROUTINE. . .
      CONV=-1.D0
      GOTO 9000

 3700 NSTRY=NSTRY+1
      RMAX=RMAX+STEPS*HH

      GOTO 5000
C
C  COMMON RETURN POINT
C  RESTORE RMAX
 9000 RMAX=RMSAVE
      IF (IWRITE) WRITE(ISCRU) IWRITE
      RETURN
      END
