      SUBROUTINE DRSET(RSTART,RSTOP,STEP,TOLHI,CAY,DRSEG,NSTEP,DR,
     1                 UNSET,IPROP,POW)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3

C  CR Le Sueur Dec 2018
C  THIS ROUTINE SETS AN INTERIM VALUE FOR DR (DRFIT) FROM EITHER
C  1) A CALCULATION INVOLVING THE WAVELENGTH (K)
C  2) THE VALUE SET IN THE NAMELIST AS EITHER DRS OR DRL (AS APPROPRIATE)
C  3) THE VALUE USED IN THE PREVIOUS SEGMENT
C
C  IT THEN CALCULATES A NUMBER OF STEPS THAT DIVIDES THE INTERVAL INTO
C  STEPS OF THIS SIZE (OR BEGINNING WITH A STEP OF THIS SIZE IF POWR/=-1)
C
C  IT THEN ADJUSTS THE VALUE OF DR SO THAT IT EXACTLY DIVIDES UP THE
C  INTERVAL (EXCEPT FOR VIVS PROPAGATION AND AIRY PROPAGATION WITH 0<TOLHI<1,
C  IN WHICH CASES THE INTERVAL SIZE IS ADJUSTED ACCORDING TO A PERTURBATION
C  THEORY CALCULATION) AND RETURNS THIS VALUE IN DR
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN)    :: RSTART,RSTOP,STEP,TOLHI,CAY,
     1                                   DRSEG,UNSET,POW
      INTEGER,          INTENT(IN)    :: IPROP
      INTEGER,          INTENT(INOUT) :: NSTEP
      DOUBLE PRECISION, INTENT(INOUT) :: DR

      DOUBLE PRECISION DRFIT,PRANGE,ZSTART,ZSTOP,ZRATIO,ZRM1,
     1                 RRATIO,DRCALC

      PRANGE=RSTOP-RSTART
C
C  OBTAIN STARTING VALUE FOR STEP LENGTH
C
      IF (IPROP.EQ.4) THEN
        IF (DRSEG.NE.UNSET) DR=DRSEG
        RETURN

      ELSEIF (STEP.GT.0.D0) THEN
C  STEP SIZE IS CALCULATED FROM STEP AND K
        DRFIT=ACOS(-1.D0)/(CAY*STEP)

      ELSEIF (DRSEG.NE.UNSET) THEN
C  STEP SIZE HAS BEEN SET IN DRS/DRL
        DRFIT=ABS(DRSEG)
C  NB THIS ASSUMES THAT DRSEG=UNSET MEANS OBTAIN FROM PREVIOUS SEGMENT

      ELSE
C  STEP SIZE IS INHERITED FROM PREVIOUS SEGMENT
        DRFIT=ABS(DR)
      ENDIF

C
C  ADJUST STEP LENGTH SO THAT RANGE IS EQUAL TO A WHOLE NUMBER OF STEPS
C  NB THIS ASSUMES THAT TOLHI AND POWR ARE SET TO 0 AND -1 IN INITIALISATION
C  IF PROPAGATOR ONLY USES FIXED STEP LENGTHS.
C
      IF (TOLHI.EQ.0.D0) THEN
        IF (POW.EQ.-1.D0) THEN
          NSTEP=MAX(NINT(ABS(PRANGE/DRFIT)),1)
          DR=PRANGE/DBLE(NSTEP)
        ELSE
C
C  NSTEP IS CALCULATED ASSUMING THAT DRFIT SPECIFIES THE SIZE OF THE
C  INNERMOST STEP
          ZSTART=MIN(RSTART,RSTOP)**(-1.D0/POW)
          ZSTOP=MAX(RSTART,RSTOP)**(-1.D0/POW)
          ZRATIO=ZSTOP/ZSTART
          ZRM1=ZRATIO-1.D0
          RRATIO=DRFIT/MIN(RSTART,RSTOP)
          NSTEP=NINT(ZRM1/((RRATIO+1.D0)**(-1.D0/POW)-1.D0))
C
C  BUT DR IS CALCULATED AS THE SIZE OF THE FIRST STEP
          DR=DRCALC(RSTART,RSTOP,0,NSTEP,POW)
        ENDIF
      ELSEIF (TOLHI.LT.1.D0) THEN
        DR=DRFIT
      ENDIF

C
C  CHANGE SIGN OF DR IF PROPAGATING INWARDS
      IF (RSTOP.LT.RSTART) DR=-ABS(DR)

      RETURN
      END
