      SUBROUTINE YTOK(NB,WVEC,L,N,NOPEN,SJ,SJP,SN,SNP,
     1                Y,T,Q,RVAL,CENT)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO OBTAIN THE K MATRIX FROM THE LOG DERIVATIVE MATRIX
C  ON ENTRY:  Y HOLDS THE LOG DERIVATIVE MATRIX
C             T IS WORK SPACE
C  DURING:    SJ,SJP HOLD THE RICCATI-BESSEL FUNCTIONS AND DERIVATIVES
C             SN,SNP HOLD THE MODIFIED SPHERICAL BESSEL FUNCTIONS AND
C             DERIVATIVES
C  ON EXIT:   Q HOLDS THE K MATRIX
C  SEE: B.R.JOHNSON, JOURNAL OF COMPUTATIONAL PHYSICS 13, 445 (1973)
C
C  EXTENDED TO NON-INTEGER ANGULAR MOMENTUM L BY M.MORITA 15-07-2014
C  IF NON-INTEGER VALUE FOUND FOR L (FROM ARRAY CENT WHICH =L(L+1))
C  ROUTINES RBESJY AND RBESSK CALLED INSTEAD OF RBES AND RMSBF
C  RESPECTIVELY.
C
      DIMENSION NB(N), WVEC(N), L(N), SJ(N), SJP(N), SN(N), SNP(N),
     1          Y(1), T(1), Q(1), CENT(N)
C
      IF (NOPEN.EQ.0) RETURN

      DO 10 I = 1,NOPEN
        NX = NB(I)
        DW = WVEC(NX)
        DARG = DW*RVAL
        REALL=SQRT(CENT(NX)+0.25D0)-0.5D0
        LL=NINT(REALL)
        IF (ABS(REALL-DBLE(LL)).LT.1D-10) THEN
          CALL RBES(LL, DARG, UJ, UJP, UN, UNP)
        ELSE
          CALL RBESJY(REALL, DARG, UJ, UJP, UN, UNP)
        ENDIF
        ROOTDW = SQRT(DW)
        SJ(NX) = UJ/ROOTDW
        SJP(NX) = UJP*ROOTDW
        SN(NX) = UN/ROOTDW
        SNP(NX) = UNP*ROOTDW
  10  CONTINUE

      IF (NOPEN.EQ.N) GOTO 30
      NCLOSE = N - NOPEN

      DO 20 I = 1,NCLOSE
        J = NOPEN + I
        NX = NB(J)
        DW = ABS(WVEC(NX))
        DARG = DW*RVAL
        REALL=SQRT(CENT(NX)+0.25D0)-0.5D0
        LL=NINT(REALL)
        IF (ABS(REALL-DBLE(LL)).LT.1D-10) THEN
          CALL RMSBF(LL, DARG, RATIO)
        ELSE
          CALL RBESSK(REALL, DARG, RATIO)
        ENDIF
        SN(NX) = 1.D0
        SNP(NX) = RATIO*DW
  20  CONTINUE
  30  CONTINUE
C
      CALL YTOKG(NB,N,NOPEN,SJ,SJP,SN,SNP,Y,T,Q)
      RETURN
      END
