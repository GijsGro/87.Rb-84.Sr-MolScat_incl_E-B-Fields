      SUBROUTINE WAVMAT(W,N,R,P,VL,IV,ERED,EINT,CENT,RMLMDA,DIAG,
     1                  MXLAM,NPOTL,IPRINT)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  EVALUATES THE POTENTIAL MATRIX W AT RADIUS R
C     W = VCOUPL + EINT + VCENT - ETOT
C  ORDER OF THE REAL SYMMETRIC MATRIX W IS N
C  THE FULL MATRIX IS COMPUTED
C  VL IS THE PREVIOUSLY COMPUTED MATRIX OF THE COUPLING POTENTIAL
C  IV IS AN INDEX ARRAY MAPPING P ONTO VL, SUCH THAT VL(I) IS
C           A COEFFICIENT TO MULTIPLY P(IV(I))
C  ERED IS THE TOTAL ENERGY ETOT IN REDUCED UNITS
C     (ETOT/EPSILON)*(2.*URED*EPSIL*RM**2/HBAR**2)
C  EINT(I) IS THE REDUCED INTERNAL ENERGY OF THE I-TH CHANNEL
C       (BUT VL ARRAY IS USED INSTEAD OF EINT IF NCONST > 0)
C  CENT(I) IS L*(L+1) FOR THE I-TH CHANNEL
C       (BUT VL ARRAY IS USED INSTEAD OF CENT IF NRSQ = 1)
C  RMLMDA IS THE SQUARE OF THE RATIO OF RM TO THE DEBROGLIE WAVELENGTH
C  AT RELATIVE ENERGY EPSILON (RMLMDA = 2.*URED*RM**2*EPSIL/HBAR**2)
C  RMLMDA MULTIPLIES THE POTENTIAL IN UNITS OF EPSIL
C
      DIMENSION W(N,N),VL(1),IV(1),EINT(N),CENT(N),P(MXLAM),DIAG(N),
     1          IDUM1(1)
C
C     COMMON /SCALE / SCALAM
C
      RSQ=1.D0/(R*R)
C  TO PREVENT OVERFLOW...
      RSQ=MIN(RSQ,1.D16)
C
C  COMPUTE THE RADIAL PARTS OF THE POTENTIAL
C  IDUM1 AND IDUM2 ARE DUMMY ARGUMENTS HERE.
      CALL POTENL(0,MXLAM,IDUM1,R,P,IDUM2,IPRINT)
      CALL PERTRB(R,P,MXLAM,0)
C
      CALL SCAPOT(P,MXLAM,RMLMDA)
C     DO 15 I=1,MXLAM
C       P(I)=P(I)*SCALAM
C       P(I)=RMLMDA*P(I)
c  15 CONTINUE
C
C  SPECIAL CODE INTRODUCED SEP 06 TO HANDLE OFF-DIAGONAL TERMS IN THE
C  THRESHOLD AND CENTRIFUGAL HAMILTONIANS.
C  THIS REPLACES IVLFL<0 IN MOLSCAT 14 AND BOUND 6 AND IS INTENDED TO BE
C  COMPATIBLE WITH NON-TRIVIAL IV.
C
      CINT=RMLMDA/EPSIL
      DO I=1,NCONST
        P(MXLAM+I)=CINT*VCONST(I)
      ENDDO

      IF (NRSQ.GT.0) THEN
        DO I=1,NRSQ
          P(MXLAM+NCONST+I)=RSQ
        ENDDO
      ENDIF
C
      CALL WAVVEC(VL,P,IV,W,N,NPOTL)
C
C  NOW COMPUTE THE DIAGONAL CONTRIBUTIONS W(I,I).
C
      IF (NRSQ.EQ.0) THEN
        DO 20 I=1,N
          W(I,I) = W(I,I) + RSQ*CENT(I)
   20   CONTINUE
      ENDIF

      IF (NCONST.EQ.0) THEN
        DO 30 I=1,N
          W(I,I) = W(I,I) + EINT(I)
   30   CONTINUE
      ENDIF

      DO 40 I=1,N
        W(I,I)  = W(I,I) - ERED
        DIAG(I) = W(I,I)
   40 CONTINUE
C

      RETURN
      END
