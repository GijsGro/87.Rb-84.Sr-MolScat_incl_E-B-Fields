      SUBROUTINE SHRINK(ICODE,RNOW,W,N,VL,IV,NB,JSINDX,L,EINT,CENT,WVEC,
     1                  CLOSED,VECOLD,EIGOLD,R,T,DEEP,NSQ,NPOTL,ISCRU,
     2                  NOPMAX,IPRINT)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  SUBROUTINE TO PERFORM A CHANNELECTOMY.
C  SHRINK REMOVES THE HIGHEST-ENERGY CHANNEL(S) FROM THE PRIMITIVE
C  BASIS SET, AND MODIFIES NUMEROUS ARRAYS TO REFLECT THIS.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER IPRINT
      DIMENSION W(1),VL(1),IV(1),NB(N),JSINDX(N),L(N),EINT(N),CENT(N),
     1          WVEC(N),CLOSED(N),VECOLD(NSQ),EIGOLD(N),R(1),T(1)
C
      COMMON /VLFLAG/ IVLFL
C
      IF (ISCRU.LE.0) GOTO 100
      IF (ICODE.EQ.1) WRITE(ISCRU) VECOLD
      IF (ICODE.EQ.2)  READ(ISCRU) VECOLD
  100 CALL TRNSP(VECOLD,N)
      CALL TRNSFM(VECOLD,R,T,N,.FALSE.,.TRUE.)
C
      DEEP2=DEEP
C
C  RUN THROUGH THE CURRENT SET OF CHANNELS FROM THE HIGHEST DOWNWARDS
      DO 1000 NNEW=N,1,-1
        IF (NNEW.LE.NOPMAX .OR. CLOSED(NNEW).LT.DEEP2) GOTO 1100
C
C  PICK UP THE LABEL OF THE CHANNEL TO BE REMOVED
        ISKIP=NB(NNEW)
C
        I=0
        INEW=0
C  REMOVE IT FROM THE W AND R ARRAYS, COMPRESSING THEM AT THE SAME TIME
        DO 200 II=1,NNEW
        DO 200 JJ=1,NNEW
          I=I+1
          IF (II.EQ.ISKIP .OR. JJ.EQ.ISKIP) GOTO 200
          INEW=INEW+1
          W(INEW)=W(I)
          R(INEW)=R(I)
  200   CONTINUE
C
        INEW=0
C  REMOVE IT FROM THE JSINDX,L,EINT,CENT,WVEC ARRAYS
        DO 300 I=1,NNEW
          IF (I.EQ.ISKIP) GOTO 300
          INEW=INEW+1
          JSINDX(INEW)=JSINDX(I)
          L(INEW)=L(I)
          EINT(INEW)=EINT(I)
          CENT(INEW)=CENT(I)
          WVEC(INEW)=WVEC(I)
  300   CONTINUE
C
        I=0
        INEW=0
C  REMOVE IT FROM THE VL ARRAY
        DO 400 II=1,NNEW
        DO 400 JJ=1,II
        DO 400 K=1,NPOTL
          I=I+1
          IF (II.EQ.ISKIP .OR. JJ.EQ.ISKIP) GOTO 400
          INEW=INEW+1
          VL(INEW)=VL(I)
          IF (IVLFL.GT.0) IV(INEW)=IV(I)
  400   CONTINUE
C
C  REMOVE IT FROM THE ENERGY ORDERING (NB) ARRAY
        DO 500 I=1,NNEW
  500     IF (NB(I).GE.ISKIP) NB(I)=NB(I)-1
C
 1000 CONTINUE
C
 1100 N=NNEW
      IF (ICODE.EQ.2) GOTO 1300
      IF (IPRINT.GE.10) WRITE(6,601) N,RNOW
  601 FORMAT('  BASIS SET CONTRACTED TO N =',I3,' AT R =',F6.2,' A')
      IFAIL=0
      CALL F02ABF(W,N,N,EIGOLD,VECOLD,N,T,IFAIL)
      IF (ISCRU.GT.0) WRITE(ISCRU) VECOLD
      GOTO 1400
C
 1300 IF (ISCRU.GT.0) READ(ISCRU) VECOLD
 1400 CALL TRNSFM(VECOLD,R,T,N,.FALSE.,.TRUE.)
      NSQ=N*N
C
      RETURN
      END
