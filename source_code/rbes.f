      SUBROUTINE RBES(N,Z,ZJN,ZJNP,ZYN,ZYNP)
C  Copyright (C) 2018 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  CALCULATES RICCATI-BESSEL FUNCTIONS OF NON-NEGATIVE INTEGER ORDER N,
C  AND POSITIVE ARGUMENT Z, AND THEIR DERIVATIVES WITH RESPECT TO Z.
C  THE DEFINITIONS AND NORMALIZATIONS ARE AS IN THE NBS HANDBOOK, P. 445
C  THE METHODS USED ARE
C        1.  IF Z.LE.N (CLASSICALLY FORBIDDEN REGION), USE
C            ASCENDING POWER SERIES FOR THE REGULAR SOLUTION ZJN AND
C            ITS DERIVATIVE ZJNP.  FORWARD RECURRENCE IS USED FOR
C            THE IRREGULAR SOLUTION ZYN AND ITS DERIVATIVE ZYNP.
C        2.  IF Z.GT.N (CLASSICALLY ALLOWED REGION), FORWARD
C            RECURRENCE IS USED FOR BOTH FUNCTIONS.
C        LIBRARY FUNCTIONS REQUIRED   SIN(Z) AND COS(Z)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DATA ONE  /1.D0/
      DATA HALF /0.5D0/
      DATA TWO  /2.D0/
      DATA ZERO /0.D0/
      DATA THREE/3.D0/
C
C  EVALUATE TRIGONOMETRIC FUNCTIONS NEEDED
      SINZ=SIN(Z)
      COSZ=COS(Z)
      ZINV=ONE/Z
      AJ=SINZ
      BJ=SINZ*ZINV-COSZ
      AY=-COSZ
      BY=-SINZ-COSZ*ZINV
      IF (N-1) 1,2,3
C  FUNCTIONS OF ORDER ZERO
1     ZJN=AJ
      ZJNP=COSZ
      ZYN=AY
      ZYNP=SINZ
      RETURN
C  FUNCTIONS OF ORDER ONE
2     ZJN=BJ
      ZJNP=AJ-ZINV*BJ
      ZYN=BY
      ZYNP=AY-ZINV*BY
      RETURN
C
C  CASES FOR ORDER GREATER THAN 1
3     XN=N
      DELF=ZINV+ZINV
      FACTOR=DELF+ZINV
C  TEST TO SEE IF RECURRENCE CAN BE USED FOR ALL FUNCTIONS
      IF (Z.GT.XN) GOTO 100
C
C  THIS CASE FOR NON-CLASSICAL REGION.
C  USE POWER SERIES FOR REGULAR SOLUTION AND RECURSION FOR IRREGULAR
C  COMPUTE COEFFICENT (C) IN FRONT OF POWER SERIES, AT SAME TIME AS
C  FORWARD RECURRENCE.
      ZSQ=Z**2
      TOP=XN+XN
      XJ=THREE
      C=ZSQ/THREE
25    ZYN=BY*FACTOR-AY
      AY=BY
      BY=ZYN
      FACTOR=FACTOR+DELF
      XJ=XJ+TWO
      C=C*(Z/XJ)
      IF (XJ.LT.TOP) GOTO 25
C  FORM DERIVATIVE OF IRREGULAR SOLUTION
      ZYNP=AY-ZINV*BY*XN
C  USE POWER SERIES FOR REGULAR SOLUTION
C  INITIALIZE
      FACTOR=-HALF*ZSQ
      U=ONE
      TERM=ONE
      DU=ONE
      DTERM=ONE
      D2NP3=TOP+THREE
      DEN=D2NP3
      DFACT=ZERO
C  INCREMENT FACTORIAL FACTOR
35    DFACT=DFACT+ONE
      TERM=TERM*(FACTOR/(DFACT*DEN))
C  SUM SERIES
      U=U+TERM
C  INCREMENT DENOMINATOR
      DEN=DEN+TWO
      DTERM=DTERM*(FACTOR/(DFACT*DEN))
C  SUM DERIVATIVE SERIES
      DU=DU+DTERM
C  TEST FOR CONVERGENCE TO SINGLE PRECISION ACCURACY
      IF (ABS(TERM).GT.1.0D-09) GOTO 35
C  CONVERGENCE.  COMPUTE REGULAR FUNCTION.
      ZJN=U*C
C  COMPUTE DERIVATIVE
      ZJNP=(XN+ONE)*ZJN*ZINV-(Z*DU/D2NP3)*C
      RETURN
C
C  CLASSICAL CASE.  USE FORWARD RECURSION.
100   CONST=ZINV*XN
      TOP=CONST+CONST
200   AJ=FACTOR*BJ-AJ
      AY=FACTOR*BY-AY
C  INCREMENT FACTOR
      FACTOR=FACTOR+DELF
C  TEST FOR COMPLETION
      IF (FACTOR.GT.TOP) GOTO 250
      BJ=FACTOR*AJ-BJ
      BY=FACTOR*AY-BY
      FACTOR=FACTOR+DELF
C  TEST FOR COMPLETION
      IF (FACTOR.LT.TOP) GOTO 200
      ZJN=BJ
      ZYN=BY
C  FORM DERIVATIVES
      ZJNP=AJ-CONST*BJ
      ZYNP=AY-CONST*BY
      RETURN
250   ZJN=AJ
      ZYN=AY
C  FORM DERIVATIVES
      ZJNP=BJ-CONST*AJ
      ZYNP=BY-CONST*AY
      RETURN
      END
